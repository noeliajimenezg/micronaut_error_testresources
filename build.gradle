plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.4.0"
    id("io.micronaut.test-resources") version "4.4.0"
}

version = "0.1"
group = "com.example"

repositories {
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        mavenContent { snapshotsOnly() }
    }
    mavenCentral()
}

configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'io.micronaut.testresources') {
            details.useVersion "2.5.3"
        }
    }
}

dependencies {
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    compileOnly("io.micronaut:micronaut-http-client")
    runtimeOnly("ch.qos.logback:logback-classic")
    testImplementation("io.micronaut:micronaut-http-client")
    testImplementation("io.micronaut.testresources:micronaut-test-resources-extensions-core")
//    manual addition of test-resources
    testImplementation("io.micronaut.testresources:micronaut-test-resources-extensions-junit-platform")
    testImplementation("io.micronaut.testresources:micronaut-test-resources-core")
    testResourcesCompileOnly("io.micronaut.testresources:micronaut-test-resources-server")
    testResourcesClient("io.micronaut.testresources:micronaut-test-resources-client")
    testResourcesService("io.micronaut.testresources:micronaut-test-resources-jdbc-oracle-free")

//  jdbc dependencies: in our case the real connection is made by a custom jdbc library
//  micronaut-data-jdbc triggers the addition of jdbc-oracle-free, only in runtimeOnly
//    runtimeOnly 'io.micronaut.data:micronaut-data-jdbc:4.8.3'
    testRuntimeOnly('io.micronaut.sql:micronaut-jdbc-hikari')
    testRuntimeOnly("com.oracle.database.jdbc:ojdbc11")
    testRuntimeOnly("org.flywaydb:flyway-database-oracle")
    testImplementation("io.micronaut.flyway:micronaut-flyway")
}


application {
    mainClass.set("com.example.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}


graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.example.*")
    }
    testResources {
        version = "2.5.3"
        enabled = project.hasProperty("micronaut.testResources") ? Boolean.parseBoolean(project.property("micronaut.testResources") as String) : false
        clientTimeout = 500 //to avoid timeouts first time pulling oracle image
//        additionalModules.add('jdbc-oracle-free') //this triggers the database if micronaut-data is in runtime classpath not test runtime classpath
    }
}

test {
    systemProperty 'micronaut.env.deduction', 'false'
    if (Boolean.parseBoolean(project.property("micronaut.testResources") as String)){
        systemProperty 'micronaut.environments', 'test'
    }
    systemProperty 'micronaut.test.resources.enabled', project.property("micronaut.testResources")
    useJUnitPlatform()
}

tasks.named("dockerfile") {
    baseImage("eclipse-temurin:21-jre-jammy")
}

tasks.named("dockerfileNative") {
    jdkVersion = "21"
}
